init {
    $svc = $lib.service.get($cmdconf.svciden)
    if $cmdopts.debug { $lib.debug = $lib.true }

    $modevt = $lib.import(zw.fileparser.evt, debug=$lib.debug)

    // make sure the meta:source node is present
    [(meta:source=$cmdconf.srcguid :name=zw.fileparser)] | spin
}

// TODO: user overridden mime
// TODO: warn when bytes aren't available

+file:bytes {
    [<(seen)+ {meta:source=$cmdconf.srcguid}]
    
    $sz = $svc.getSize(:sha256)
    if $sz {[:size=$sz]}
    
    $hashes = $svc.getHashes(:sha256)
    if $hashes {[:md5=$hashes.md5 :sha1=$hashes.sha1 :sha256=$hashes.sha256 :sha512=$hashes.sha512]}

    $mime = $svc.getMime(:sha256)
    if $mime {[:mime=$mime] {[(file:ismime=($node,$mime) <(seen)+ {meta:source=$cmdconf.srcguid})]} }

    for $evt in $svc.parseFile(:sha256) {
        yield $modevt.handleEvt($evt)
    }
}